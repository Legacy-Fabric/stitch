import com.diffplug.gradle.spotless.JavaExtension

plugins {
	id "java"
	id "java-library"
	id "maven-publish"
	id 'com.diffplug.spotless' version "6.12.0"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def ENV = System.getenv()
version = "0.6.3" + (ENV.GITHUB_ACTIONS ? "" : "+local")

group = 'net.legacyfabric'
archivesBaseName = project.name.toLowerCase()

repositories {
	mavenCentral()
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}
}

configurations {
	ship
	enigma
	implementation.extendsFrom ship
	compileOnly.extendsFrom enigma
	testImplementation.extendsFrom enigma
}

dependencies {
	ship 'org.ow2.asm:asm:9.1'
	ship 'org.ow2.asm:asm-commons:9.1'
	ship 'org.ow2.asm:asm-tree:9.1'
	ship 'org.ow2.asm:asm-util:9.1'
	ship 'net.fabricmc:tiny-mappings-parser:0.3.0+build.17'
	implementation 'com.google.guava:guava:28.0-jre'
	compileOnly 'org.jetbrains:annotations:20.1.0'

	enigma 'cuchaz:enigma:0.23.2'

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
}

spotless {
	format('java', JavaExtension) {
		licenseHeaderFile(rootProject.file("HEADER"))
		target '**/*.java'
	}
}

jar {
	manifest {
		attributes 'Implementation-Title': 'Stitch',
				'Implementation-Version': archiveVersion,
				'Main-Class': "net.fabricmc.stitch.Main"
	}
}

task allJar(type: Jar) {
	from {
		configurations.ship.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
	manifest {
		attributes 'Implementation-Title': 'Stitch',
				'Implementation-Version': archiveVersion,
				'Main-Class': "net.fabricmc.stitch.Main"
	}
	archiveClassifier = 'all'
	with jar
}

java {
	withSourcesJar()
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	if (JavaVersion.current().isJava9Compatible()) it.options.release = 8
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact(allJar)
		}
	}

	// select the repositories you want to publish to
	repositories {
		if (ENV.MAVEN_PUBLISH_CREDENTIALS) {
		    maven {
			url "https://repo.legacyfabric.net/repository/legacyfabric"
			credentials {
			    username ENV.MAVEN_PUBLISH_CREDENTIALS.split(":")[0]
			    password ENV.MAVEN_PUBLISH_CREDENTIALS.split(":")[1]
			}
			authentication {
			    basic(BasicAuthentication)
			}
		    }
		}
	}
}

test {
	useJUnitPlatform()
}

// A task to ensure that the version being released has not already been released.
task checkVersion {
	doFirst {
		def url = new URL("https://repo.legacyfabric.net/repository/legacyfabric/net/legacyfabric/stitch/maven-metadata.xml")
		if (url != null) {
			def xml = url.text
			if (xml != null && !xml.isEmpty()) {
				def metadata = new XmlSlurper().parseText(xml)
				def versions = metadata.versioning.versions.version*.text();
				if (versions.contains(version)) {
					throw new RuntimeException("${version} has already been released!")
				}
			}
		}
	}
}

publish.mustRunAfter checkVersion
